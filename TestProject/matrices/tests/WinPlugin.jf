#TestCase;#Kind;#Depends;#For
Application start;Never;;
    #Step;#Kind;#For;#Depends
    'Start without any parameters';;;
        #Id;#Action;#Assert;$AppId
        APPSTR1;ApplicationStart;This.Result == Result.Failed && This.Kind == ErrorKind.EXCEPTION;'WIN'

    #EndStep

    #Step;#Kind;#For;#Depends
    'Start without workdir';;;
        #Id;#Action;#Assert;$AppId;Exec
        APPSTR2;ApplicationStart;This.Result == Result.Failed && This.Kind == ErrorKind.EXCEPTION;'WIN';'calc.exe'

    #EndStep

    #Step;#Kind;#For;#Depends
    'Start with invalid exec';;;
        #Id;#Action;#Assert;$AppId;WorkDir;Exec
        APPSTR3;ApplicationStart;This.Result == Result.Failed && This.Kind == ErrorKind.EXCEPTION;'WIN';'.';new Date()

    #EndStep

    #Step;#Kind;#For;#Depends
    'Start with invalid workdir';;;
        #Id;#Action;$AppId;WorkDir;Exec
        APPSTR3;ApplicationStart;'WIN';new Date();'calc.exe'

        #Id;#Action;$AppConnection
        APPSTP1;ApplicationStop;APPSTR3.Out

    #EndStep

    #Step;#Kind;#For;#Depends
    'start with valid parameters';;;
        #Id;#Action;$AppId;WorkDir;Exec
        APPSTR3;ApplicationStart;'WIN';mockWinDir;mockWinExec

        #Id;#Action;$AppConnection
        APPSTP1;ApplicationStop;APPSTR3.Out

    #EndStep


#TestCase;#Kind;#Depends;#For
Application connect;;;
    #Id;#Action;$AppId;WorkDir;Exec
    app;ApplicationStart;'WIN';mockWinDir;mockWinExec

    #Id;#Action;$AppConnection;Rectangle
    APPPAR2;ApplicationGetProperties;app.Out;

    #Id;#Let
    pid;app.Out.getProcessId()

    #Id;#Let
    h;APPPAR2.Out.Rectangle.height

    #Id;#Let
    w;APPPAR2.Out.Rectangle.width

    #Step;#Kind;#For;#Depends
    'Connect via title';;;
        #Id;#Action;$AppId;MainWindow
        APPSTR4;ApplicationConnectTo;'WIN';'MockWin'

        #Id;#Action;$AppConnection
        APPSTP2;ApplicationStop;APPSTR4.Out

    #EndStep

    #Step;#Kind;#For;#Depends
    'Connect via size';;;
        #Id;#Action;$AppId;Height;Width
        APPSTR4;ApplicationConnectTo;'WIN';h;w

        #Id;#Action;$AppConnection
        APPSTP2;ApplicationStop;APPSTR4.Out

    #EndStep

    #Step;#Kind;#For;#Depends
    'Connecti via pid';;;
        #Id;#Action;$AppId;PID
        APPSTR4;ApplicationConnectTo;'WIN';pid

        #Id;#Action;$AppConnection
        APPSTP2;ApplicationStop;APPSTR4.Out

    #EndStep

    #Step;#Kind;#For;#Depends
    'Connecti via title and pid';;;
        #Id;#Action;$AppId;WorkDir;Exec
        newApp;ApplicationStart;'WIN';mockWinDir;mockWinExec

        #Id;#Action;#Assert;$AppId;MainWindow
        APPSTR1;ApplicationConnectTo;This.Result == Result.Failed && This.Kind == ErrorKind.EXCEPTION;'WIN';'MockWin'

        #Id;#Action;$AppId;PID;MainWindow
        APPSTR1;ApplicationConnectTo;'WIN';newApp.Out.getProcessId();'MockWin'

        #Id;#Action;$AppConnection
        APPSTP3;ApplicationStop;APPSTR1.Out

        #Id;#Action;$AppConnection
        APPSTP4;ApplicationStop;newApp.Out

    #EndStep

    #Id;#Action;$AppConnection
    APPSTP1;ApplicationStop;app.Out

    #Step;#Kind;#For;#Depends
    'Connect with timeout';;;
        #Id;#RawText;#Kind
        TEXT1;Text;Matrix
        ~;"// "
        ~;"#TestCase;#Kind;#Depends;#For"
        ~;"Test case;Never;;"
        ~;"    #Action;$Time"
        ~;"    Wait;5000"
        ~;""
        ~;"    #Id;#Action;$Command;$Wait"
        ~;"    EXEC1;Execute;'notepad.exe';false"
        #EndRawText

        #Id;#Action;$Text
        MXRN1;MatrixRunFromText;TEXT1

        #Id;#Action;$AppId;MainWindow;Timeout
        APPSTR1;ApplicationConnectTo;'WIN';'Untitled';30000

        #Id;#Action;$AppConnection;$Kill
        APPSTP1;ApplicationStop;APPSTR1.Out;true

    #EndStep

    #Step;#Kind;#For;#Depends
    'Connect with timeout 2';;;
        #Id;#RawText;#Kind
        TEXT1;Text;Matrix
        ~;"// "
        ~;"#TestCase;#Kind;#Depends;#For"
        ~;"Test case;Never;;"
        ~;"    #Action;$Time"
        ~;"    Wait;5000"
        ~;""
        ~;"    #Id;#Action;Exec;WorkDir;$AppId"
        ~;"    APPSTR1;ApplicationStart;mockWinExec;mockWinDir;'WIN'"
        ~;""
        ~;"    #Action;$Value;$Name"
        ~;"    Store;APPSTR1.Out;'appConnection'"
        #EndRawText

        #Id;#Action;$Text
        MXRN1;MatrixRunFromText;TEXT1

        #Id;#Action;$AppId;MainWindow;Timeout
        APPSTR1;ApplicationConnectTo;'WIN';'MockWin';30000

        #Id;#Action;$AppConnection;$Kill
        APPSTP7;ApplicationStop;APPSTR1.Out;true

        #Action;$Name;$AsVar
        Restore;'appConnection';'appC'

        #Id;#Action;$AppConnection
        APPSTP8;ApplicationStop;appC

    #EndStep


#TestCase;#Kind;#Depends;#For
Application stop;;;
    #Step;#Kind;#For;#Depends
    'Start - stop';;;
        #Id;#Action;$AppId;WorkDir;Exec
        app;ApplicationStart;'WIN';mockWinDir;mockWinExec

        #Id;#Action;$AppConnection
        APPSTP5;ApplicationStop;app.Out

    #EndStep

    #Step;#Kind;#For;#Depends
    'connect - stop';;;
        #Id;#Action;$AppId;WorkDir;Exec
        app;ApplicationStart;'WIN';mockWinDir;mockWinExec

        #Id;#Action;$AppId;PID
        APPSTR5;ApplicationConnectTo;'WIN';app.Out.getProcessId()

        #Id;#Action;$AppConnection
        APPSTP6;ApplicationStop;APPSTR5.Out

        #Id;#Action;$AppConnection
        APPSTP5;ApplicationStop;app.Out

    #EndStep

    #Step;#Kind;#For;#Depends
    'connect - stop with kill';;;
        #Id;#Action;$AppId;WorkDir;Exec
        app;ApplicationStart;'WIN';mockWinDir;mockWinExec

        #Id;#Action;$AppId;PID
        APPSTR5;ApplicationConnectTo;'WIN';app.Out.getProcessId()

        #Id;#Action;$AppConnection;$Kill
        APPSTP6;ApplicationStop;APPSTR5.Out;true

        #Id;#Action;#Assert;$AppConnection;Rectangle
        APPPAR1;ApplicationGetProperties;This.Result == Result.Failed && This.Kind == ErrorKind.EXCEPTION;app.Out;

        #Id;#Action;$AppConnection
        APPSTP5;ApplicationStop;app.Out

    #EndStep


#TestCase;#Kind;#Depends;#For
Application get properties;;;
    #Id;#Global;#Action;$AppId;WorkDir;Exec
    appGP;1;ApplicationStart;'WIN';mockWinDir;mockWinExec

    #Id;#Action;$AppConnection;Rectangle
    APPPAR2;ApplicationGetProperties;appGP.Out;

    #Id;#Let
    width;APPPAR2.Out.Rectangle.width

    #Id;#Let
    height;APPPAR2.Out.Rectangle.height

    #Assert;#Message
    width > 800 && width < 1000;'Width is  wrong ' + width

    #Assert;#Message
    height > 800 && height < 1000;'Height is  wrong ' + height

    #Id;#Action;$AppConnection;Title
    APPPAR3;ApplicationGetProperties;appGP.Out;

    #Assert;#Message
    APPPAR3.Result == Result.Passed;


#TestCase;#Kind;#Depends;#For
Application get properties (close app);;;
    #Id;#Action;$AppConnection
    APPSTP5;ApplicationStop;appGP.Out


#TestCase;#Kind;#Depends;#For
Dialog get properties;;;
    #Id;#Step;#Kind;#For;#Depends
    Start;;;;
        #Id;#Global;#Action;Exec;WorkDir;Args;AlwaysToFront;$AppId
        APPSTR2;1;ApplicationStart;mockWinExec;mockWinDir;'';true;mockWinApp

    #EndStep

    #Id;#Step;#Kind;#For;#Depends
    Positive;;;;
        #Id;#Action;#Assert;$Size;$Position;$Dialog;$AppConnection
        APPPAR2;DialogGetProperties;This.Result == Result.Passed && This.Out.Size != null && This.Out.Position != null;;;'Dialog';APPSTR2.Out

        #Action;#Assert;$Size;$Dialog;$AppConnection
        DialogGetProperties;This.Result == Result.Passed && This.Out.Size != null;;'Dialog';APPSTR2.Out

        #Action;#Assert;$Position;$Dialog;$AppConnection
        DialogGetProperties;This.Result == Result.Passed && This.Out.Position != null;;'Dialog';APPSTR2.Out

        #Action;#Assert;$Dialog;$AppConnection
        DialogGetProperties;This.Result == Result.Passed && This.Out.size == 0;'Dialog';APPSTR2.Out

    #EndStep

    #Id;#Step;#Kind;#For;#Depends
    Negative;;;;
        #Id;#Action;#Assert;$Dialog;$AppConnection
        APPPAR7;DialogGetProperties;This.Result == Result.Failed && This.Kind == ErrorKind.DIALOG_NOT_FOUND;'Dialogg';APPSTR2.Out

        #Id;#Action;#Assert;$Dialog;$AppConnection
        APPPAR8;DialogGetProperties;This.Result == Result.Failed && This.Kind == ErrorKind.EMPTY_PARAMETER;'Dialog';

        #Id;#Action;#Assert;$Dialog;$AppConnection
        APPPAR9;DialogGetProperties;This.Result == Result.Failed && This.Kind == ErrorKind.EMPTY_PARAMETER;;APPSTR2.Out

        #Id;#Action;#Assert;$Dialog;$AppConnection
        APPPAR10;DialogGetProperties;This.Result == Result.Failed && This.Kind == ErrorKind.EXCEPTION;'Dialog';asd

    #EndStep

    #Id;#Step;#Kind;#For;#Depends
    Stop;;;;
        #Action;$AppConnection
        ApplicationStop;APPSTR2.Out

    #EndStep


