/*******************************************************************************
 * Copyright (c) 2009-2018, Exactpro Systems LLC
 * www.exactpro.com
 * Build Software to Test Software
 *
 * All rights reserved.
 * This is unpublished, licensed software, confidential and proprietary
 * information which is the property of Exactpro Systems LLC or its licensors.
 ******************************************************************************/

import com.ullink.Msbuild

apply plugin: 'com.ullink.msbuild'
apply plugin: 'com.ullink.nuget'

description = "UIAdapter"

def dllExportVersion = '1.6.0' // TODO get version from packages.config

task copyDllExportBat(type: Copy) {
    dependsOn nugetRestore
    
    from "packages/DllExport.${dllExportVersion}/DllExport.bat"
    into '.'
}

class CSharpLibraryBuild extends Msbuild {

    @InputFiles
    Iterable<File> getSourceFiles() {
        this.project.fileTree(this.project.file(this.projectFile).parentFile) {
	        include '**/*.cs'
	        include '*.csproj'
	        exclude 'obj/**'
	        exclude 'bin/**'
	    }
    }
    
    @OutputFiles
    Iterable<File> getOutputFiles() {
        def projectFile = this.project.file(this.projectFile)
        def projectDirName = projectFile.parent
        def projectName = projectFile.name.replaceAll(/\..+/, '')
        def output = this.project.files("$projectDirName/bin/$configuration/${projectName}.dll") 
        if (this.configuration == 'Debug') {
            output = output + this.project.files("$projectDirName/bin/$configuration/${projectName}.pdb")
        }
        return output
    }
}

task csbuild(type: CSharpLibraryBuild) {
    dependsOn copyDllExportBat
    onlyIf {
        System.getProperty('os.name').toLowerCase().contains('windows')
    }
    
    projectFile = 'UIAdapter/UIAdapter.csproj'
	platform = 'x64'
	configuration = 'Debug'
    verbosity = 'detailed'
    parameters.SolutionDir = project.projectDir.path + java.io.File.separator
    ext["flp1"] = "LogFile=" + file("${project.name}.errors.log").path + ";ErrorsOnly;Verbosity=diag"
}
